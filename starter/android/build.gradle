buildscript {
    ext.kotlin_version = '1.9.10'
    ext.javaVersion = JavaVersion.VERSION_1_8
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'

// flutter 3.24 many packages haven't upgraded to 3.24 android tools so we have to override
subprojects {
    afterEvaluate { project ->
        if (project.extensions.findByName("android") != null) {
            Integer pluginCompileSdk = project.android.compileSdk
            if (pluginCompileSdk != null && pluginCompileSdk < 34) {
                project.android {
                    compileSdk 34
                    if (android.namespace == null) {
                        namespace project.group
                    }
                    compileOptions {
                        sourceCompatibility javaVersion
                        targetCompatibility javaVersion
                    }
                }

                
                tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                    kotlinOptions {
                        jvmTarget = javaVersion.toString()
                    }
                }
            }
        }
    }
    
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
